<?xml version="1.0" encoding="utf-8"?>
<mx:TitleWindow
      xmlns:mx="http://www.adobe.com/2006/mxml"
      layout="absolute"
      width="400" height="300"
      title="@Resource(key='SelectFilter_title', bundle='messages')"
      creationComplete="init()">
	<mx:Script>
		<![CDATA[
			import mx.core.Application;
			import mx.managers.PopUpManager;
			import mx.collections.ArrayCollection;
			import org.ziptie.flex.services.ResultElf;
			import mx.rpc.events.ResultEvent;
			import org.ziptie.flex.services.WebServiceElf;

            [Bindable]
            private var _filters:ArrayCollection;

            private var _current:String;
            private var _onResult:Function;

            public static function select(onResult:Function, current:String):void
            {
            	var d:SelectFilter = PopUpManager.createPopUp(Application.application.mainPage, SelectFilter, true) as SelectFilter;
            	d._current = current == '' ? null : current;
            	d._onResult = onResult;
            	PopUpManager.centerPopUp(d);
            }

			private function init():void
			{
				WebServiceElf.call('scheduler', 'getFilterNames', filterNames);
			}

            private function filterNames(event:ResultEvent):void
            {
            	_filters = ResultElf.array(event);
            	_table.selectedItem = _current;
            }

            private function ok():void
            {
            	_onResult(_table.selectedItem);
            	PopUpManager.removePopUp(this);
            }
		]]>
	</mx:Script>

    <mx:List
          id="_table"
          styleName="popupVBox"
          dataProvider="{_filters}"
          change="btnOk.enabled = _table.selectedItem != null"
          width="100%" height="100%"/>

    <mx:ControlBar horizontalAlign="right">
        <mx:Button id="btnOk" label="@Resource(key='ok', bundle='messages')" click="ok()"/>
        <mx:Button label="@Resource(key='cancel', bundle='messages')" click="PopUpManager.removePopUp(this)"/>
    </mx:ControlBar>
</mx:TitleWindow>
