<?xml version="1.0" encoding="utf-8"?>
<mx:Canvas
      xmlns:mx="http://www.adobe.com/2006/mxml"
      creationComplete="init()">
    <mx:Metadata>
        [Event(name='change', type='flash.events.Event')]
    </mx:Metadata>
    <mx:Script>
    	<![CDATA[
    		import mx.collections.ArrayCollection;
    		import mx.containers.GridRow;
    		import mx.controls.Label;
    		import mx.rpc.events.ResultEvent;
    		import org.ziptie.flex.services.ResultElf;
    		import org.ziptie.flex.services.WebServiceElf;

            private var _valueControls:Object;
            private var _credentialSet:Object;

    		public function set credentialSet(credentialSet:Object):void
    		{
    			_credentialSet = credentialSet;

            	var credentials:ArrayCollection = ResultElf.array(credentialSet.credentials);

                credentialSet.credentials = credentials;
                if (_valueControls != null)
                {
                	var used:Object = new Object();
                	for each (var credential:Object in credentials)
                	{
                        var control:CredentialValueControl = _valueControls[credential.name];
                        control.credential = credential;
                        used[credential.name] = 1;
                	}

                    for (var key:Object in _valueControls)
                    {
                    	if (used[key] == 1)
                    	{
                    		continue;
                    	}

	                	var cred:Object = {
                            id:-1,
                            name:key,
                            value:''
                        };

	                	_valueControls[key].credential = cred;

	                	credentials.addItem(cred);
                    }
                }
    		}

            private function init():void
            {
            	WebServiceElf.call('adapters', 'getCredentialKeys', credentialKeys);
            }

            private function credentialKeys(event:ResultEvent):void
            {
            	_valueControls = new Object();

                for each (var credentialKey:Object in ResultElf.array(event))
                {
                    var cvc:CredentialValueControl = new CredentialValueControl();
                    cvc.addEventListener(Event.CHANGE, valueChanged);
                    cvc.credentialKey = credentialKey;
                    _grid.addChild(cvc);

                    _valueControls[credentialKey.keyName] = cvc;
                }

                if (_credentialSet != null)
                {
                	credentialSet = _credentialSet;
                }
            }

            private function valueChanged(event:Event):void
            {
            	dispatchEvent(new Event(Event.CHANGE));
            }
    	]]>
    </mx:Script>
    <mx:Grid id="_grid" width="100%" height="100%" />
</mx:Canvas>
